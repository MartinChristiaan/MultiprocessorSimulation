import "RingFlit.poosl"
import "../common/Fifo.poosl"
process class TDMRouter (ProcessingTime: Real,Xpos : Integer,NumberOfXNodes: Integer)
ports
	In, IXL1,IXL2, Out, OXR1,OXR2
messages
	In?Flit(RingFlit),
	IXL1?Flit(RingFlit),
	IXL2?Flit(RingFlit),	
	Out!Flit(RingFlit),
	OXR1!Flit(RingFlit),
	OXR2!Flit(RingFlit)
variables
	Buffer1sTurn : Boolean
	Buffer2sTurn : Boolean
init
	MyInit()()
	
methods
	ManageTurns()()
		Buffer1sTurn := true;
		delay(ProcessingTime*10.0);
		Buffer1sTurn := false;
		delay(ProcessingTime);
		Buffer2sTurn := true;
		delay(ProcessingTime*10.0);
		Buffer2sTurn := false;
		delay(ProcessingTime);
		ManageTurns()()

	TransferFlit1()() |f: Flit, fx : Integer|
		
		/* Receives and forwards a flit for the positive direction */		
		sel
			In?Flit(f)
		or
			IXL1?Flit(f)
		les;
		[Buffer1sTurn]		
		delay(ProcessingTime);
		fx := f getXCoordinate(); 
		if  fx- Xpos == 0 then
			Out!Flit(f)
		else 
			OXR1!Flit(f)
		fi;		
		TransferFlit1()()
	
	TransferFlit2()() |f: Flit, fx : Integer|
		IXL2?Flit(f);
		[Buffer2sTurn]
		delay(ProcessingTime);
		fx := f getXCoordinate(); 
		if fx -Xpos == NumberOfXNodes then
			Out!Flit(f)
		else
 			OXR2!Flit(f)			
		fi;		
		TransferFlit2()()
		
	MyInit()()
		Buffer1sTurn := false;
		Buffer2sTurn := false;
		par
			TransferFlit1()()
		and
			TransferFlit2()()
		and
			ManageTurns()()
		rap
		
		